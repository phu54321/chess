import loc;
import board;

function move(unitPlayer, unitCellX, unitCellY, currentTurn, cb: EUDFuncPtr(4, 0)) {
  const opponentPlayer = (unitPlayer == $P7) ? $P8 : $P7;
  const pawnDy = (unitPlayer == $P7) ? 1 : -1;
  const pawnStartingY = (unitPlayer == $P7) ? 2 : 7;
  const enPassentY = (unitPlayer == $P7) ? 5 : 4;

  // 처음에 2칸
  if (unitCellY == pawnStartingY) {
    if(
      board.getBoard(unitCellX, unitCellY + pawnDy)[[0]] == 0 &&
      board.getBoard(unitCellX, unitCellY + pawnDy * 2)[[0]] == 0
    ) {
      cb(unitCellX, unitCellY + pawnDy * 2, $U('Cursor'), unitPlayer);
    }
  }

  // 한 칸 밑으로 간다
  const promotionY = (unitPlayer == $P7) ? 7 : 2;
  if (1 <= unitCellY + pawnDy && unitCellY + pawnDy <= 8) {
    const cursorUnit = (promotionY == unitCellY) ? $U('Promotion') : $U('Cursor');
    if(board.getBoard(unitCellX, unitCellY + pawnDy)[[0]] == 0) {
      cb(unitCellX, unitCellY + pawnDy, cursorUnit, unitPlayer);
    }
    if(unitCellX > 1 && board.getBoard(unitCellX - 1, unitCellY + pawnDy)[[0]] == opponentPlayer) {
      cb(unitCellX - 1, unitCellY + pawnDy, cursorUnit, unitPlayer);
    }
    if(unitCellX < 8 && board.getBoard(unitCellX + 1, unitCellY + pawnDy)[[0]] == opponentPlayer) {
      cb(unitCellX + 1, unitCellY + pawnDy, cursorUnit, unitPlayer);
    }
  }

  // 앙파상
  if (unitCellY == enPassentY) {
    if (unitCellX > 1) {
      const p, ut, lmt = board.getBoard(unitCellX - 1, unitCellY);
      if (p == opponentPlayer && ut == $U('Pawn') && lmt == currentTurn - 1) {
        cb(unitCellX - 1, unitCellY + pawnDy, $U('EnPassent'), unitPlayer);
      }
    }

    if (unitCellX < 8) {
      const p, ut, lmt = board.getBoard(unitCellX + 1, unitCellY);
      if (p == opponentPlayer && ut == $U('Pawn') && lmt == currentTurn - 1) {
        cb(unitCellX + 1, unitCellY + pawnDy, $U('EnPassent'), unitPlayer);
      }
    }
  }
}
